autoload -U colors compinit

colors
compinit


# History
# -------

HISTFILE=~/.zsh_history

HISTSIZE=1000
SAVEHIST=1000

setopt appendhistory
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt extended_history
setopt hist_verify
setopt inc_append_history
setopt no_share_history


# Aliases
# -------

if which xdg-open > /dev/null; then
  alias open='xdg-open'
fi

alias ls='ls --color'

alias l='ls -lah'
alias ll='ls -lh'
alias la='ls -lAh'

alias h='history -i'

alias activate='source env/bin/activate'

setopt extendedglob
setopt nomatch

# Key bindings
# ------------

bindkey -v

bindkey '^a' beginning-of-line
bindkey '^e' end-of-line
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word
bindkey '^r' history-incremental-search-backward
bindkey '^p' up-history
bindkey '^n' down-history

bindkey '^o' accept-and-infer-next-history


# Completion
# ----------

unsetopt menu_complete
unsetopt flowcontrol

setopt auto_menu
setopt complete_in_word
setopt always_to_end

zstyle ':compinstall' filename '/home/benoit/.zshrc'

zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*' matcher-list 'r:|=*' 'l:|=* r:|=*'


# Misc
# ----

setopt notify
setopt no_autocd
setopt no_beep


# Prompt
# ------

function prompt_cwd() {
  print "%{$fg[blue]%}%3~%{$reset_color%} "
}

function prompt_caret() {
  print "%(?,%{$fg[green]%},%{$fg[red]%})%%%{$reset_color%} "
}

function prompt_git() {
  BRANCH=$(git rev-parse --abbrev-ref HEAD 2> /dev/null)
  STATUS=$(git status --porcelain 2> /dev/null | tail -n1)

  if [[ -n $BRANCH ]]; then
    if [[ -n $STATUS ]]; then
      print "$BRANCH%{$fg[red]%}!%{$reset_color%} "
    else
      print "$BRANCH "
    fi
  fi
}

function prompt_env() {
  if [[ -n $VIRTUAL_ENV ]]; then
    print "(%{$fg[yellow]%}$(basename "$VIRTUAL_ENV")%{$reset_color%}) "
  fi
}

function set_title {
  if [[ $TERM == "screen"* ]]; then
    print -nR $'\033]0;'$2$'\a'
  fi
}

function set_prompt() {
  export PS1="$(prompt_cwd)$(prompt_git)$(prompt_env)$(prompt_caret)"
}

precmd() {
  set_title "$PWD"
  set_prompt
}


# Environment
# -----------

export WORDCHARS=''

export PATH=~/bin:$PATH

export EDITOR='vim'
export PAGER='less'
